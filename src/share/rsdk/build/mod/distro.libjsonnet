local distro_check = import "../../configs/distro_check.libjsonnet";

local mirror_link(
    distro_mirror,
    snapshot_timestamp,
    distro,
    architecture,
    distro_mirror_default,
    snapshot_mirror_default
) = if snapshot_timestamp != ""
then
    // Build with snapshot service
    snapshot_mirror_default + snapshot_timestamp
else
    (if distro_mirror == ""
     then
         // Primary archive
         distro_mirror_default
     else
         distro_mirror) + "/" +
     (if distro == "ubuntu" && architecture != "amd64"
      then
          distro + "-ports"
      else
          distro);

local distro_base(
    suite,
    distro_mirror,
    distro,
    architecture,
    snapshot_timestamp,
    distro_mirror_default,
    snapshot_mirror_default,
) = {
    mmdebstrap+: {
        components+: [
            "main",
        ],
        mirrors+: [
            mirror_link(distro_mirror,
                        snapshot_timestamp,
                        distro,
                        architecture,
                        distro_mirror_default,
                        snapshot_mirror_default),
        ],
        "setup-hooks"+: [
            |||
                # updates archive
                head -n 1 "$1/etc/apt/sources.list" | sed -E -e "s/(%(suite)s)/\\1-updates/" > "$1/etc/apt/sources.list.d/50-%(suite)s-updates.list"
                # backports archive
                head -n 1 "$1/etc/apt/sources.list" | sed -E -e "s/(%(suite)s)/\\1-backports/" > "$1/etc/apt/sources.list.d/50-%(suite)s-backports.list"
                # security archive
                head -n 1 "$1/etc/apt/sources.list" | sed -E -e "s/(%(suite)s)/\\1-security/" -e "s|/debian|/debian-security|" > "$1/etc/apt/sources.list.d/50-%(suite)s-security.list"
                # main archive
                mv "$1/etc/apt/sources.list" "$1/etc/apt/sources.list.d/50-%(suite)s.list"
            ||| % {
                suite: suite
            }
        ] + 
(if suite == "bullseye"
then
        [
            |||
                sed -i "s|https*://.*/|http://archive.debian.org/|" "$1/etc/apt/sources.list.d/50-%(suite)s-backports.list"
            ||| % {
                suite: suite
            }
        ]
else
        []
),
        suite+: suite,
    }
};

local distro_debian(
    suite,
    distro_mirror,
    architecture,
    snapshot_timestamp,
    distro = "debian",
) = distro_base(suite, distro_mirror, distro, architecture, snapshot_timestamp, "https://deb.debian.org", "https://snapshot.debian.org/archive/debian/") + {
    mmdebstrap+: {
        components+: std.prune([
            "contrib",
            "non-free",
            if suite == "bookworm" || suite == "trixie" then "non-free-firmware",
        ])
    }
};

local distro_ubuntu(
    suite,
    distro_mirror,
    architecture,
    snapshot_timestamp,
    distro = "ubuntu",
) = distro_base(suite, distro_mirror, distro, architecture, snapshot_timestamp, "http://ports.ubuntu.com", "https://snapshot.ubuntu.com/ubuntu/") + {
    mmdebstrap+: {
        components+: [
            "restricted",
            "universe",
            "multiverse",
        ]
    }
};

function(suite, distro_mirror, architecture, snapshot_timestamp)
    if distro_check(suite) == "debian"
    then
        distro_debian(suite, distro_mirror, architecture, snapshot_timestamp)
    else if distro_check(suite) == "ubuntu"
    then
        distro_ubuntu(suite, distro_mirror, architecture, snapshot_timestamp)
