local soc_family = import "../../configs/soc_family.libjsonnet";
local product_soc = import "../../configs/product_soc.libjsonnet";
local soc_specific_repo = import "../../configs/soc_specific_repo.libjsonnet";
local product_soc_family(product) = soc_family(product_soc(product));
local extra_ppa = import "../../configs/extra_ppa.libjsonnet";
local distro_check = import "../../configs/distro_check.libjsonnet";
local vscodium = import "vscodium.libjsonnet";

// Return Radxa base URL. If empty, use official host (with trailing slash).
// Do not change or normalise user-provided URLs in any way.
local radxa_url(radxa_mirror) = (
    if radxa_mirror == ""
    then
        "https://radxa-repo.github.io/"
    else
        radxa_mirror
);

// suite: Debian/Ubuntu codename (e.g. bookworm, jammy) that selects the base distro
// radxa_mirror: Radxa repository mirror base URL; empty string means the official default
// radxa_repo_suffix: optional suffix appended to suite for Radxa repos (e.g. "-testing")
// product: product identifier (e.g. rock-3c), used to derive SoC and family
// temp_dir: temporary directory containing local debs and build artefacts
// install_vscodium: whether to add the VSCodium repository
// use_pkgs_json: whether to download and embed Radxa pkgs.json metadata
function(suite, radxa_mirror, radxa_repo_suffix, product, temp_dir, install_vscodium, use_pkgs_json) {
    // Pre-compute common parameters to avoid repeated string concatenation below
    local base_radxa_url = radxa_url(radxa_mirror),
    local full_suite = suite + radxa_repo_suffix,
    local soc = product_soc(product),
    local family = product_soc_family(product),

    // 80-radxa-* sources: SoC-specific or family repositories; only write deb entries, independent of pkgs.json
    local radxa_80_sources = if soc_specific_repo(soc)
        then [
            |||
                echo deb %(radxa_url)s%(product_soc)s-%(suite)s %(product_soc)s-%(suite)s main > "$1/etc/apt/sources.list.d/80-radxa-%(product_soc)s.list"
            ||| % {
                    radxa_url: base_radxa_url,
                    suite: full_suite,
                    product_soc: soc,
                }
        ]
        else [
            |||
                echo deb %(radxa_url)s%(suite)s %(product_soc_family)s-%(suite)s main > "$1/etc/apt/sources.list.d/80-radxa-%(product_soc_family)s.list"
            ||| % {
                    radxa_url: base_radxa_url,
                    suite: full_suite,
                    product_soc_family: family,
                }
        ],

    // 70-radxa: main Radxa repository deb entry; always present, pkgs.json controlled by hooks below
    local radxa_70_source = [
        |||
            echo deb %(radxa_url)s%(suite)s %(suite)s main > "$1/etc/apt/sources.list.d/70-radxa.list"
        ||| % {
                radxa_url: base_radxa_url,
                suite: full_suite,
            },
    ],

    // Radxa pkgs.json downloads and snapshot; grouped in a single hooks list controlled by use_pkgs_json
    local radxa_pkgs_json_hooks = if use_pkgs_json then
        (if soc_specific_repo(soc)
            then [
                |||
                    wget -O "$1/etc/rsdk/80-radxa-%(product_soc)s.pkgs.json" %(radxa_url)s%(product_soc)s-%(suite)s/pkgs.json
                ||| % {
                        radxa_url: base_radxa_url,
                        suite: full_suite,
                        product_soc: soc,
                    }
            ]
            else [
                |||
                    wget -O "$1/etc/rsdk/80-radxa-%(product_soc_family)s.pkgs.json" %(radxa_url)s%(suite)s/pkgs.json
                ||| % {
                        radxa_url: base_radxa_url,
                        suite: full_suite,
                        product_soc_family: family,
                    }
            ]
        )
        + [
            |||
                wget -O "$1/etc/rsdk/70-radxa.pkgs.json" %(radxa_url)s%(suite)s/pkgs.json
            ||| % {
                    radxa_url: base_radxa_url,
                    suite: full_suite,
                },
            'curl -L -o "$1/etc/radxa_apt_snapshot" %(radxa_url)s%(suite)s/pkgs.json'
                % {
                        radxa_url: base_radxa_url,
                        suite: full_suite,
                    },
        ]
    else
        [],

    mmdebstrap+: {
        packages+: [
            "radxa-archive-keyring",
            "local-apt-repository",
        ],
        "setup-hooks"+: [
            'mkdir -p "$1/etc/rsdk/"',
            |||
                set -e
                mkdir -p "$1/srv/"
                cp -R "%(temp_dir)s/debs/." "$1/srv/local-apt-repository"
                cp -R "%(temp_dir)s/build/." "$1/"
                cd "$1/srv/local-apt-repository"
                apt-ftparchive packages . > ./Packages
                apt-ftparchive -o "APT::FTPArchive::Release::Origin=local-apt-repository" release . > ./Release
                cd -
                echo "deb [trusted=yes] file://$1/srv/local-apt-repository ./" > "$1/etc/apt/sources.list.d/99-local-apt-repository.list"
                cat << EOF > "$1/etc/apt/preferences.d/local-apt-repository"
                Package: *
                Pin: release o=local-apt-repository
                Pin-Priority: 1999
                EOF
            ||| % {
                temp_dir: temp_dir,
            },
        ]
        + radxa_80_sources
        + radxa_70_source
        + radxa_pkgs_json_hooks,
        "essential-hooks"+: (if suite != "trixie"
        then
            [
                |||
                    APT_CONFIG="$MMDEBSTRAP_APT_CONFIG" \
                    apt-get install -oDPkg::Chroot-Directory="$1" -y \
                    software-properties-common
                |||
            ]
        else
            []
        )
        + (if distro_check(suite) == "ubuntu"
            then
                [
                    |||
                        chroot "$1" add-apt-repository -y -n "ppa:mozillateam/ppa"
                    |||,
                ]
            else
                []
        )
        + (if std.length(extra_ppa(product)) > 0
        then
            [
                |||
                    chroot "$1" add-apt-repository -y -n "ppa:%(ppa)s"
                ||| % {
                    ppa: ppa,
                } for ppa in extra_ppa(product)
            ]
        else 
            []
        )
        + [
            |||
                APT_CONFIG="$MMDEBSTRAP_APT_CONFIG" \
                apt-get update -oDPkg::Chroot-Directory="$1"
            |||,
        ],
        "customize-hooks"+: [
            |||
                set -e
                export SYSTEMD_RELAX_ESP_CHECKS=1

                APT_CONFIG="$MMDEBSTRAP_APT_CONFIG" apt-get update -oDPkg::Chroot-Directory="$1"
                APT_CONFIG="$MMDEBSTRAP_APT_CONFIG" DEBIAN_FRONTEND=noninteractive NEEDRESTART_SUSPEND=1 apt-get full-upgrade -oDPkg::Chroot-Directory="$1" -y --allow-downgrades
                APT_CONFIG="$MMDEBSTRAP_APT_CONFIG" DEBIAN_FRONTEND=noninteractive NEEDRESTART_SUSPEND=1 apt-get autoremove -oDPkg::Chroot-Directory="$1" -y --purge
                sed -i "s|^deb|deb [signed-by=\"/usr/share/keyrings/radxa-archive-keyring.gpg\"]|g" "$1"/etc/apt/sources.list.d/*-radxa*.list
                rm "$1/etc/apt/sources.list.d/99-local-apt-repository.list"
                rm "$1/srv/local-apt-repository/Packages"
                rm "$1/srv/local-apt-repository/Release"
                rm "$1"/fixup-*
            |||,
        ],
    }
} + (if install_vscodium
then
    vscodium(suite, radxa_mirror)
else
    {}
)
