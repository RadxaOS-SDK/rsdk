local distro_check = import "../../../configs/distro_check.libjsonnet";
local desktop_packages = import "categories/desktop.libjsonnet";
local product_soc = import "../../../configs/product_soc.libjsonnet";

function(suite,
         product,
         temp_dir,
         vendor_packages,
         linux_override,
         u_boot_override,
) desktop_packages(suite,
                product,
                temp_dir,
                vendor_packages,
                linux_override,
                u_boot_override,
) + {
    mmdebstrap+: {
        packages+:
        [
            "accountwizard",
            "bluedevil",
            "dragonplayer",
            "kamera",
            "kde-config-plymouth",
            "kde-config-screenlocker",
            "kde-config-sddm",
            "kdeconnect",
            "khotkeys",
            "kinfocenter",
            "kio-extras",
            "konqueror",
            "kscreen",
            "kup-backup",
            "kwin-x11",
            "libkf5kdelibs4support5",
            "libkf5kdelibs4support5-bin",
            "phonon4qt5-backend-gstreamer",
            "phonon4qt5settings",
            "plasma-discover",
            "plasma-nm",
            "powerdevil",
            "print-manager",
            "qtvirtualkeyboard-plugin",
            "qml-module-org-kde-newstuff",
            "qml-module-qt-labs-platform",
            "sddm-theme-breeze",
            "sonnet-plugins",
            "systemsettings",
            "yakuake",
            "xdg-desktop-portal-kde",
        ] +

(if distro_check(suite) == "debian"
then
        [
            "task-kde-desktop",
        ]
else if distro_check(suite) == "ubuntu"
then
        [
            "kubuntu-desktop",
        ]
else
        []
) +

(if suite != "focal"
then
        [
            "kde-config-systemd",
        ]
else
        []
) +

(if suite == "bookworm" && std.startsWith(product_soc(product), "rk3588")
then
        // Install Debian 12 packages
        [
            "plasma-workspace-wayland",
            "qml-module-org-kde-pipewire",
            // Debian 12's sddm won't turn the screen on boot
            "gdm3",
        ]
else
        []
),
        "customize-hooks"+:
(if suite == "bookworm" && std.startsWith(product_soc(product), "rk3588")
then
        [
            // Manually set gdm3 as the default display manager, as dpkg-reconfigure does not work under chroot
            |||
                echo "gdm3 shared/default-x-display-manager select gdm3" | chroot "$1" debconf-set-selections
                echo "/usr/sbin/gdm3" > "$1/etc/X11/default-display-manager"
                ln -sf "/lib/systemd/system/gdm3.service" "$1/etc/systemd/system/display-manager.service"
            |||
        ]
else
        []
),
    },
}
