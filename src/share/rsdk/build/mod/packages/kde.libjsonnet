local distro_check = import "../../../configs/distro_check.libjsonnet";
local desktop_packages = import "categories/desktop.libjsonnet";
local product_soc = import "../../../configs/product_soc.libjsonnet";
local soc_family = import "../../../configs/soc_family.libjsonnet";
local gdm = import "gdm.libjsonnet";

function(suite,
         product,
         temp_dir,
         vendor_packages,
         linux_override,
         firmware_override,
) desktop_packages(suite,
                product,
                temp_dir,
                vendor_packages,
                linux_override,
                firmware_override,
) + {
    mmdebstrap+: {
        packages+:
        [
            "accountwizard",
            "bluedevil",
            "dragonplayer",
            "kamera",
            "kde-config-plymouth",
            "kde-config-screenlocker",
            "kde-config-sddm",
            "kdeconnect",
            "khotkeys",
            "kinfocenter",
            "kio-extras",
            "konqueror",
            "konsole",
            "kscreen",
            "kup-backup",
            "kwin-x11",
            "libkf5kdelibs4support5",
            "libkf5kdelibs4support5-bin",
            "phonon4qt5settings",
            "plasma-discover",
            "plasma-nm",
            "powerdevil",
            "print-manager",
            "qtvirtualkeyboard-plugin",
            "qml-module-org-kde-newstuff",
            "qml-module-qt-labs-platform",
            "sddm-theme-breeze",
            "sonnet-plugins",
            "systemsettings",
            "task-kde-desktop",
            "yakuake",
            "xdg-desktop-portal-kde",
        ] +

(if suite != "focal"
then
        [
            "kde-config-systemd",
        ]
else
        []
) +

(if suite == "bookworm"
then
        // Install Debian 12 packages
        [
            "plasma-workspace-wayland",
            "qml-module-org-kde-pipewire",
        ]
else
        []
) +

(if product_soc(product) == "a733"
then
        // Allwinner A733 GPU driver only support OpenGL ES,
        // so use libqt5gui5-gles instead of libqt5gui5
        // to make KDE use hardware acceleration, but it
        // conflicts with phonon4qt5-backend-gstreamer's dependency
        [
            "libqt5gui5-gles",
        ]
else
        [
            "phonon4qt5-backend-gstreamer",
        ]
),
    },
} + (if suite == "bookworm"
then
    // Debian 12's sddm has issue handling screen hotplug, as well as screen wake up
    // https://applink.feishu.cn/client/message/link/open?token=AmY%2FRdMxxQABZoS475OOwAQ%3D
    // https://vamrs.feishu.cn/sheets/IFXSs271ThaBVytTCVbcMcxsnpc?sheet=0KtklS&rangeId=0KtklS_iOZ0zYhC18&rangeVer=1
    gdm()
else
    {}
)
